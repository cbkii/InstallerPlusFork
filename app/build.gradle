plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 35
    buildToolsVersion '35.0.0'

    // Load keystore properties if available (for CI signing)
    def keystorePropsFile = rootProject.file('keystore.properties')
    def keystoreProperties = new Properties()
    if (keystorePropsFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropsFile))
    }

    signingConfigs {
        release {
            if (keystorePropsFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "ltd.nextalone.pkginstallerplus"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode(getTimeStamp.call() as int)
        versionName "1.2" + (getGitHeadRefsSuffix.call() as String)
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (keystorePropsFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    aaptOptions {
        additionalParameters '--allow-reserved-package-id', '--package-id', '0x75'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'ltd.nextalone.pkginstallerplus'
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
}
