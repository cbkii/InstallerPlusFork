plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 36
    buildToolsVersion '36.0.0'

    defaultConfig {
        applicationId "ltd.nextalone.pkginstallerplus"
        minSdkVersion 23
        targetSdkVersion 36
        versionCode(getTimeStamp.call() as int)
        versionName "2.0" + (getGitHeadRefsSuffix.call() as String)
    }

    signingConfigs {
        release {
            // Load keystore from keystore.properties file (created by GitHub Actions)
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Apply signing config to release builds if keystore.properties exists
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    aaptOptions {
        additionalParameters '--allow-reserved-package-id', '--package-id', '0x75'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    namespace 'ltd.nextalone.pkginstallerplus'
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
}
