plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 34
    buildToolsVersion '34.0.0'

    defaultConfig {
        applicationId "ltd.nextalone.pkginstallerplus"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode(getTimeStamp.call() as int)
        versionName "2.0" + (getGitHeadRefsSuffix.call() as String)
    }

    signingConfigs {
        release {
            // These will be populated from GitHub Secrets via environment variables
            storeFile file(System.getenv("SIGNING_KEYSTORE_PATH") ?: "release.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Enable signing if credentials are available
            if (System.getenv("SIGNING_STORE_PASSWORD")) {
                signingConfig signingConfigs.release
            }
        }
    }
    aaptOptions {
        additionalParameters '--allow-reserved-package-id', '--package-id', '0x75'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    namespace 'ltd.nextalone.pkginstallerplus'
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
}
